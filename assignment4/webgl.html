<!doctype html>
<html lang='en'>
    <head>
        <style>body{ margin:0; background: black; }</style>
    </head>
    <body>
        <canvas id='gl'></canvas>
    </body>

    <script id='copyVertex' type='x-shader/x-vertex'>#version 300 es
        in vec2 a_pos;

        void main() {
            gl_Position = vec4( a_pos, 0, 1 );
        }
    </script>

    <!-- render to screen shader -->
    <script id='copyFragment' type='x-shader/x-fragment'>#version 300 es
        #ifdef GL_ES
        precision mediump float;
        #endif

        uniform sampler2D uSampler;
        uniform vec2 resolution;

        out vec4 color;
        void main() {
            vec2 pos = gl_FragCoord.xy / resolution;
            vec4 tex = texture( uSampler, pos );
            color = vec4( tex.rgb, 1. );
        }
    </script>

    <script id='simulationVertex' type='x-shader/x-vertex'>#version 300 es
        precision mediump float;
        in vec4 a_pos;
        
        uniform sampler2D uSampler;

        uniform vec2 resolution;
        out vec4 o_vpos;

        vec2 dirs[4] = vec2[4](
            vec2(1.,0),
            vec2(0,1.),
            vec2(-1.,0),
            vec2(0,-1.)
        );

        void main() {
            // our vertex positions are reported from -1 to 1.
            // this converts that to 0 to 1, which is needed for
            // our texture lookup
            vec2 texCoord = (1. + a_pos.xy) * .5;
            float pheromone = texture( uSampler, texCoord ).r;
            
            // initialize our vertex transform output
            o_vpos = vec4( a_pos[0], a_pos[1], a_pos[2], pheromone);
            
            // how big is each square in the representation?
            float gridSize = 8.;
            
            // how far the vant should travel (but not the direction)
            float dist = (2. / resolution.x) * gridSize;
            
            // if pheromones were found
            if( pheromone > 0. ) {
                o_vpos[2] += -1.; // turn 90 degrees counter-clockwise
                o_vpos[3] = 1.;   // set pheromone flag
            }else{
                o_vpos[2] += 1.;  // turn clockwise
                o_vpos[3] = 0.;   // unset pheromone flag
            }
            
            // wrap direction lookup
            if( o_vpos[2] < 0. ) o_vpos[2] = 3.;
            
            // get direction from our array
            vec2 newdir = dirs[ int( mod( o_vpos[2], 4. ) ) ];
            
            gl_PointSize = gridSize;
            
            // set position for transform feedback
            o_vpos.xy += newdir * dist; 
            
            // send current position to fragment shader
            gl_Position = vec4( a_pos.x, a_pos.y, 0., 1. );
        }
    </script>

    <script id='simulationFragment' type='x-shader/x-fragment'>#version 300 es
        precision mediump float;
        
        uniform vec2 resolution;

        in  vec4 o_vpos;
        out vec4 o_frag;
        
        void main() {
            // color red for no pheremone, blue for pheremone
            vec3 color = o_vpos[3] == 0. ? vec3(1.,0.,0.) : vec3(0.,0.,1.);
            o_frag = vec4( vec3( color ), 1. );
        } 
    </script>
    
    <script src="https://cdn.jsdelivr.net/npm/regl@2/dist/regl.min.js"></script>
    <script src="https://cdn.jsdelivr.net/npm/tweakpane@3/dist/tweakpane.min.js"></script>

    <script src="main.js"></script>

</html>